import com.bmuschko.gradle.docker.tasks.image.*
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'gradle-one-jar'
apply plugin: 'application'
apply plugin: 'com.bmuschko.docker-remote-api'

// UTF-8 should be standard by now. So use it!
//[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'


// Add Gradle OneJar Plugin, see https://github.com/rholder/gradle-one-jar
buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath 'com.github.rholder:gradle-one-jar:1.0.3'
        classpath 'com.bmuschko:gradle-docker-plugin:3.0.6'
    }
}

// Set our project variables
project.ext {
    dropwizardVersion = '1.0.5'
}

repositories {
    mavenCentral()
}

dependencies {
    compile('com.google.maps:google-maps-services:0.1.16')
    compile( project(':shopping-service-lib')){
        exclude group:'org.springframework.boot'
    }
    compile("io.dropwizard:dropwizard-core:${dropwizardVersion}")
    compile("io.dropwizard:dropwizard-hibernate:${dropwizardVersion}")
    compile("io.dropwizard:dropwizard-migrations:${dropwizardVersion}")
    compile("io.dropwizard:dropwizard-auth:${dropwizardVersion}")
    compile("io.dropwizard:dropwizard-assets:${dropwizardVersion}")

    compile("com.h2database:h2:1.3.168")

}

// The main class of the application
mainClassName = 'me.mikael.shopping.ShoppingApplication'

task bundleWebapp(dependsOn:':shopping-web-a2:ngBuild', type: Copy){
    from rootProject.file('shopping-web-a2/dist')
    into file("${project.buildDir}/one-jar-build/assets")
    include '**/*'
}

// Configure the oneJar task
task oneJar(dependsOn: 'bundleWebapp', type: OneJar) {
    mainClass = mainClassName
}

task copyForDocker(type: Copy) {
    dependsOn   'oneJar'
    println "build/libs/${project.name}-${jar.version}-standalone.jar"

    from  "build/libs/${project.name}-${jar.version}-standalone.jar"
    into 'build/docker'
    rename { String fileName ->
        println "replacing version bit in $fileName"
        fileName.replace("-${jar.version}-standalone", "")
    }

    from 'src/docker/Dockerfile'
    into 'build/docker'
    //filter(ReplaceTokens, tokens: [fileName: '2009', version: '2.3.1'])

    from 'src/dist/config/config.yml'
    into 'build/docker'

}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn   copyForDocker

    //dockerFile = file('src/docker/Dockerfile')
    inputDir = file('build/docker')
    if(!inputDir.exists()){
        assert inputDir.mkdirs()
    }
    tag = project.name
}

// Configure the run task to start the Dropwizard service
run {
    args 'server', './src/dist/config/config.yml'

}

artifacts {
    oneJar
}